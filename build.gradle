plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.4'
	id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.akfnt'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '21'
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2023.0.1")
	set('otelVersion', "1.32.0")
}

dependencies {
	// 스프링 클라우드 함수 의존성과 스프링 부트 스타터 의존성의 경우 스프링 클라우드 스트림에 이미 포함되어 있으므로 제거
	implementation 'org.springframework.cloud:spring-cloud-stream-binder-rabbit'
	implementation 'org.springframework.cloud:spring-cloud-starter-config'
	implementation 'org.springframework.retry:spring-retry'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	// actuator 는 HTTP 를 통해 엔드포인트를 제공하도록 설정된 웹 서버가 필요하므로 webflux 의존성을 추가
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	compileOnly 'org.projectlombok:lombok:1.18.32'
	annotationProcessor 'org.projectlombok:lombok:1.18.32'
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	runtimeOnly "io.opentelemetry.javaagent:opentelemetry-javaagent:${otelVersion}"		// 바이트코드를 통해 동적으로 자바 코드를 계측하는 오픈텔레메트리 에이전트

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	// 테스트 바인더 의존성
	testImplementation("org.springframework.cloud:spring-cloud-stream-test-binder")
	testCompileOnly 'org.projectlombok:lombok:1.18.32'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.32'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

// 애플리케이션을 컨테이너화 하기 위한 설정
// 빌드팩을 사용해 OCI 이미지를 빌드하기 위한 스프링 부트 플러그인 작업
// 컨테이너 이미지를 빌드하고 깃허브 저장소에 업로드: ./gradlew bootBuildImage --imageName ghcr.io/akfnt/cns-dispatcher-service --publishImage -PregistryUrl="ghcr.io" -PregistryUsername=akfnt -PregistryToken=
bootBuildImage {
	// 빌드할 OCI 이미지의 이름. 이 이름은 그래들의 프로젝트 설정에서 정의한 이름과 같다.
	// 버전 번호를 사용하지 않고 '암묵적인 latest 태그'(prod 환경에서는 권장하지 않음)를 사용한다.
	imageName = "${project.name}"
	// 이미지에 설치할 JVM 버전. 자바 21을 사용한다.
	environment = ["BP_JVM_VERSION" : "21.*"]

	// 컨테이너 저장소 연결을 설정하기 위한 섹션
	docker {
		// 컨테이너 저장소 인증을 설정하기 위한 섹션. 값은 그래들 속성을 통해 전달된다.
		publishRegistry {
			username = project.findProperty("registryUsername")
			password = project.findProperty("registryToken")
			url = project.findProperty("registryUrl")
		}
	}
}